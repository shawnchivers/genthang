// Synchronized upscaler to HDMI
// Based on framebuffer_sync.sv from SNESTang / MDTang (nand2mario, 2024, GPL-3.0);
// modified for Gen Thang (scanlines, composite blend, Genesis DAC table).
//
// This module takes RGB pixel input, buffers 16 lines of the video in BRAM, 
// then upscales and output the video in 720p HDMI format.
// The video aspect ratio is 4:3, with the video centered on the screen.
//
// This is different from framebuffer.sv in that it requires the input video
// to be synchronized to HDMI. This allows the module use much less BRAM space
// compared to a full framebuffer.
module framebuffer #(
    parameter WIDTH = 320,          // max frame width (<=1280)
    parameter HEIGHT = 240,         // max frame height (<=720)
    parameter COLOR_BITS = 4,       // bits per RGB color channel
    parameter BUFFER_LINES = 32     // power-of-two line ring, must exceed core lead over HDMI
)(
	input clk,                      // megadrive clock
	input resetn,
	input clk_pixel,                // 74.25Mhz pixel clock
	input clk_5x_pixel,             // 5x pixel clock

    // video signals
    input ce_pix,                   // pixel strobe
    input [COLOR_BITS-1:0] r,       // pixel color
    input [COLOR_BITS-1:0] g,
    input [COLOR_BITS-1:0] b,
    input transp,                   // VDP transparency-dither flag of this pixel (adaptive blend)
    input interlace,                // quasi-static; scanlines are skipped in interlace like MiSTer
    input [$clog2(WIDTH)-1:0] x,    // pixel position
    input [$clog2(HEIGHT)-1:0] y,
    input [10:0] width,             // frame width
    input [9:0] height,             // frame height

    output pause_core,              // sync mechanism: input video should be a bit faster (e.g. 0.x%)
                                    // than HDMI. It should wait after line 0 is rendered
                                    // and wait for sync_line1 to continue rendering.

    // overlay from iosys
    input overlay,
    output [7:0] overlay_x,
    output [7:0] overlay_y,
    input [14:0] overlay_color,     // BGR5
    input [3:0] video_opt,          // [1:0] scanlines off/25%/50%, [3:2] composite blend off/on/adaptive

    input [15:0] audio_left,        // audio sample
    input [15:0] audio_right,

	// HDMI output signals
	output       tmds_clk_n,
	output       tmds_clk_p,
	output [2:0] tmds_d_n,
	output [2:0] tmds_d_p
);

localparam CLKFRQ = 74250;

// video stuff
wire [10:0] cx /* xsynthesis syn_keep=1 */;
wire [9:0] cy  /* xsynthesis syn_keep=1 */;

//
// BRAM frame buffer
//
localparam FB_DEPTH = WIDTH * BUFFER_LINES;
localparam COLOR_WIDTH = COLOR_BITS * 3;
localparam MEM_WIDTH = COLOR_WIDTH + 1;     // {transp, r, g, b}
localparam FB_AWIDTH = $clog2(FB_DEPTH);
localparam LINE_BITS = $clog2(BUFFER_LINES);
reg [MEM_WIDTH-1:0] mem [0:FB_DEPTH-1];
reg [FB_AWIDTH-1:0] mem_portA_addr;
reg mem_portA_we;

wire [FB_AWIDTH-1:0] mem_portB_addr;
reg [MEM_WIDTH-1:0] mem_portB_rdata;

// BRAM port A read/write
reg ce_pix_r, hblank_r;
assign mem_portA_addr = y[LINE_BITS-1:0] * WIDTH + x;
always @(posedge clk) begin
    ce_pix_r <= ce_pix;
    if (ce_pix & ~ce_pix_r) 
        mem[mem_portA_addr] <= {transp, r, g, b};
end

// BRAM port B read
reg [$clog2(WIDTH)-1:0] xx  /* xsynthesis syn_keep=1 */; // scaled-down pixel position
reg [$clog2(HEIGHT)-1:0] yy /* xsynthesis syn_keep=1 */;
assign mem_portB_addr = yy[LINE_BITS-1:0] * WIDTH + xx;
always @(posedge clk_pixel) begin
    mem_portB_rdata <= mem[mem_portB_addr];
end

// Video synchronization
reg hdmi_first_line;
frame_sync sync (
    .clk(clk), .resetn(resetn), .x(x), .y(y), .hdmi_first_line(hdmi_first_line),
    .pause_core(pause_core)
);
always @(posedge clk_pixel) begin
    if (cy == 0 && cx >= 160)                           // start position of 4:3 frame
        hdmi_first_line <= 1;
    else
        hdmi_first_line <= 0;
end

// audio stuff
localparam AUDIO_RATE=48000;
localparam AUDIO_CLK_DELAY = CLKFRQ * 1000 / AUDIO_RATE / 2;
logic [$clog2(AUDIO_CLK_DELAY)-1:0] audio_divider;
logic clk_audio;

always @(posedge clk_pixel) begin
    if (audio_divider != AUDIO_CLK_DELAY - 1) 
        audio_divider++;
    else begin 
        clk_audio <= ~clk_audio; 
        audio_divider <= 0; 
    end
end

reg [15:0] audio_sample_word [1:0];
always @(posedge clk) begin
    audio_sample_word[0] <= audio_left;
    audio_sample_word[1] <= audio_right;
end

//
// Video
//
reg [23:0] rgb;             // actual RGB output
reg active                  /* xsynthesis syn_keep=1 */;
reg [10:0] xcnt             /* xsynthesis syn_keep=1 */;
reg [10:0] ycnt             /* xsynthesis syn_keep=1 */;                  // fractional scaling counters
reg [9:0] cy_r;
assign overlay_x = xx;
assign overlay_y = yy;
localparam XSTART = (1280 - 960) / 2;   // 960:720 = 4:3
localparam XSTOP = (1280 + 960) / 2;

// address calculation
// Assume the video occupies fully on the Y direction, we are upscaling the video by `720/height`.
// xcnt and ycnt are fractional scaling counters.
always @(posedge clk_pixel) begin
    reg active_t;
    reg [10:0] xcnt_next;
    reg [10:0] ycnt_next;
    xcnt_next = xcnt + (overlay ? 256 : width);
    ycnt_next = ycnt + (overlay ? 224 : height);

    active_t = 0;
    if (cx == XSTART - 1) begin
        active_t = 1;
        active <= 1;
    end else if (cx == XSTOP - 1) begin
        active_t = 0;
        active <= 0;
    end

    if (active_t | active) begin        // increment xx
        xcnt <= xcnt_next;
        if (xcnt_next >= 960) begin
            xcnt <= xcnt_next - 960;
            xx <= xx + 1;
        end
    end

    cy_r <= cy;
    if (cy[0] != cy_r[0]) begin         // increment yy at new lines
        ycnt <= ycnt_next;
        if (ycnt_next >= 720) begin
            ycnt <= ycnt_next - 720;
            yy <= yy + 1;
        end
    end

    if (cx == 0) begin
        xx <= 0;
        xcnt <= 0;
    end
    
    if (cy == 0) begin
        yy <= 0;
        ycnt <= 0;
    end 

end

// calc rgb value to hdmi
// Game pixels go through MiSTer's measured Genesis DAC table. Blend averages each pixel
// with the one to its left (composite dither blending; adaptive = only where the VDP
// flags transparency dither). Scanlines dim the lower half of each Genesis line.
reg [3:0] opt_s1, opt_s2;
reg il_s1, il_s2;
reg [$clog2(WIDTH)-1:0] xx_r, xx_px;
reg [COLOR_WIDTH-1:0] rdata_r, prev_px;
reg scan;
// rdata holds Genesis pixel xx_r; it just moved to a new pixel when xx_r != xx_px
wire [COLOR_WIDTH-1:0] left_px = xx_r != xx_px ? rdata_r : prev_px;
always @(posedge clk_pixel) begin
    opt_s1 <= video_opt;
    opt_s2 <= opt_s1;
    il_s1 <= interlace;
    il_s2 <= il_s1;
    scan <= ycnt >= 11'd360 && !il_s2;
    xx_r <= xx;
    xx_px <= xx_r;
    rdata_r <= mem_portB_rdata[COLOR_WIDTH-1:0];
    if (!active)
        prev_px <= mem_portB_rdata[COLOR_WIDTH-1:0];    // pixel 0 is its own left neighbour
    else
        prev_px <= left_px;
end

function automatic [7:0] dac(input [3:0] i);
    dac = {i, i};
endfunction

function automatic [7:0] chan(input [COLOR_BITS-1:0] a, input [COLOR_BITS-1:0] b,
                              input blend, input [1:0] dim);
    reg [8:0] s;
    reg [7:0] v;
    s = blend ? dac(a) + dac(b) : {dac(a), 1'b0};
    v = s[8:1];
    case (dim)
    2'd1:    chan = v - v[7:2];
    2'd2:    chan = {1'b0, v[7:1]};
    default: chan = v;
    endcase
endfunction

wire [1:0] dim = scan ? opt_s2[1:0] : 2'd0;
wire do_blend = opt_s2[3:2] == 2'd1 || (opt_s2[3:2] == 2'd2 && mem_portB_rdata[COLOR_WIDTH]);

always @(posedge clk_pixel) begin
    if (active) begin
        if (overlay)
            rgb <= {overlay_color[4:0], overlay_color[4:2], overlay_color[9:5], overlay_color[9:7],
                    overlay_color[14:10], overlay_color[14:12]};                      // BGR5 to RGB8
        else
            rgb <= {chan(mem_portB_rdata[COLOR_BITS*2 +: COLOR_BITS], left_px[COLOR_BITS*2 +: COLOR_BITS], do_blend, dim),
                    chan(mem_portB_rdata[COLOR_BITS   +: COLOR_BITS], left_px[COLOR_BITS   +: COLOR_BITS], do_blend, dim),
                    chan(mem_portB_rdata[0            +: COLOR_BITS], left_px[0            +: COLOR_BITS], do_blend, dim)};
    end else
        rgb <= 24'h000000;
end

// HDMI output.
logic[2:0] tmds;

localparam FRAMEWIDTH = 1280;
localparam FRAMEHEIGHT = 720;
localparam TOTALWIDTH = 1650;
localparam TOTALHEIGHT = 750;
localparam SCALE = 5;
localparam VIDEOID = 4;
localparam VIDEO_REFRESH = 60.0;
localparam COLLEN = 80;
localparam AUDIO_BIT_WIDTH = 16;
hdmi #( .VIDEO_ID_CODE(VIDEOID), 
        .DVI_OUTPUT(0),
        .VIDEO_REFRESH_RATE(VIDEO_REFRESH),
        .IT_CONTENT(1),
        .AUDIO_RATE(AUDIO_RATE), 
        .AUDIO_BIT_WIDTH(AUDIO_BIT_WIDTH),
        .START_X(0),
        .START_Y(0) )

hdmi( .clk_pixel_x5(clk_5x_pixel), 
        .clk_pixel(clk_pixel), 
        .clk_audio(clk_audio),
        .rgb(rgb), 
        .reset( /* ~resetn */ ),
        .audio_sample_word(audio_sample_word),
        .tmds(tmds), 
        .tmds_clock(tmdsClk), 
        .cx(cx), 
        .cy(cy),
        .frame_width(),
        .frame_height() );

// Gowin LVDS output buffer
ELVDS_OBUF tmds_bufds [3:0] (
    .I({clk_pixel, tmds}),
    .O({tmds_clk_p, tmds_d_p}),
    .OB({tmds_clk_n, tmds_d_n})
);

endmodule
